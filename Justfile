[private]
default: system-info
  @just --list

[unix]
lint:
  @echo "ğŸ“  Linting Nix config..."
  @nix --extra-experimental-features 'nix-command flakes' fmt
  @echo "âœ…  Nix config linted!"

[unix]
check-aarch64-darwin:
  @echo "ğŸ”  Checking Nix config for aarch64-darwin..."
  @NIXPKGS_ALLOW_UNSUPPORTED_SYSTEM=1 NIXPKGS_ALLOW_UNFREE=1 NIXPKGS_ALLOW_BROKEN=1 nix --extra-experimental-features 'nix-command flakes' flake check --impure --system aarch64-darwin --show-trace
  @echo "âœ…  Check completed for aarch64-darwin!"

[unix]
check-x86_64-linux:
  @echo "ğŸ”  Checking Nix config for x86_64-linux..."
  @NIXPKGS_ALLOW_UNSUPPORTED_SYSTEM=1 NIXPKGS_ALLOW_UNFREE=1 NIXPKGS_ALLOW_BROKEN=1 nix --extra-experimental-features 'nix-command flakes' flake check --impure --system x86_64-linux --show-trace
  @echo "âœ…  Check completed for x86_64-linux!"

[unix]
check-aarch64-linux:
  @echo "ğŸ”  Checking Nix config for aarch64-linux..."
  @NIXPKGS_ALLOW_UNSUPPORTED_SYSTEM=1 NIXPKGS_ALLOW_UNFREE=1 NIXPKGS_ALLOW_BROKEN=1 nix --extra-experimental-features 'nix-command flakes' flake check --impure --system aarch64-linux --show-trace
  @echo "âœ…  Check completed for aarch64-linux!"

[unix]
check: system-info
  @if [ "$(arch)" = "aarch64" ] && [ "$(os)" = "macos" ]; then \
    just check-aarch64-darwin; \
  elif [ "$(arch)" = "x86_64" ] && [ "$(os)" = "linux" ]; then \
    just check-x86_64-linux; \
  elif [ "$(arch)" = "aarch64" ] && [ "$(os)" = "linux" ]; then \
    just check-aarch64-linux; \
  else \
    echo "Unsupported architecture and OS combination"; \
  fi

[unix]
flake-update:
  @echo "ğŸ”„  Updating flake..."
  @nix --extra-experimental-features 'nix-command flakes' flake update --option access-tokens "github.com=$(gh auth token)"
  @echo "âœ…  Flake updated!"

[unix]
update-pins dry_run='':
  @echo "ğŸ”„  Updating pinned flake inputs..."
  @{{ if dry_run == "--dry-run" { "DRY_RUN=true" } else { "" } }} scripts/update-flake-pins.sh
  @echo "âœ…  Pins updated!"

[unix]
setup-python-packages packages='':
  @echo "ğŸ”„  Setting up Python packages..."
  @scripts/setup-python-packages.sh {{packages}}
  @echo "âœ…  Python packages setup!"

[unix]
update-python-packages:
  @echo "ğŸ”„  Updating Python packages..."
  @scripts/update-python-packages.sh --no-build
  @echo "âœ…  Python packages updated!"

[unix]
update-node-packages:
  @echo "ğŸ”„  Updating Node packages..."
  @scripts/update-node-packages.sh
  @echo "âœ…  Node packages updated!"

[unix]
nix-gc:
  @echo "ğŸ§¹  Garbage collecting nix..."
  @nix-env --delete-generations 14d
  @nix-store --gc
  @nix-collect-garbage -d
  @echo "âœ…  Garbage collected!"

[private]
build-macos fast='':
  @echo "ğŸ”¨  Building Nix config for macOS ğŸ"
  @{{ if fast != "--fast" { "just lint flake-update" } else { "echo \"ğŸš€ Fast mode, skipping checks...\"" } }}
  sudo darwin-rebuild switch --flake .#Joes-MacBook-Pro
  @echo "âœ…  Built for macOS!"

[macos]
build-macos-fast:
  @echo "ğŸ”¨  Building Nix config for macOS ğŸ (fast mode)"
  sudo darwin-rebuild switch --flake .#Joes-MacBook-Pro
  @echo "âœ…  Built for macOS!"

[macos]
build-macos-initial:
  @echo "ğŸ”¨  Building Nix config for macOS ğŸ (fast mode)"
  sudo nix --extra-experimental-features 'nix-command flakes' run nix-darwin -- switch --flake .#Joes-MacBook-Pro
  @echo "âœ…  Built for macOS!"

[macos]
organize-launchpad:
  @echo "ğŸ”¨  Organizing Launchpad..."
  @lporg load --config $(pwd)/hosts/common/system/dotconfig/lporg.yaml --yes --no-backup
  @echo "âœ…  Organized Launchpad!"

[macos]
save-launchpad:
  @echo "ğŸ”¨  Organizing Launchpad..."
  @lporg save --config $(pwd)/hosts/common/system/dotconfig/lporg.yaml
  @echo "âœ…  Saved Launchpad!"

[macos]
build fast='': system-info
  @just build-macos {{fast}}

[private]
build-wsl fast='':
  @echo "ğŸ”¨  Building Nix config for WSL ğŸªŸ"
  @{{ if fast != "--fast" { "just flake-update" } else { "echo \"ğŸš€ Fast mode, skipping checks...\"" } }}
  @{{ if fast != "--fast" { "echo \"ğŸ”  Checking Nix config for WSL...\"" } else { "" } }}
  @{{ if fast != "--fast" { "NIXPKGS_ALLOW_UNSUPPORTED_SYSTEM=1 NIXPKGS_ALLOW_UNFREE=1 NIXPKGS_ALLOW_BROKEN=1 nix --extra-experimental-features 'nix-command flakes' flake check --impure --system x86_64-linux --show-trace" } else { "" } }}
  sudo nixos-rebuild --flake .#joe-wsl switch
  @echo "âœ…  Built for WSL!"

[private]
build-wsl-fast:
  @echo "ğŸ”¨  Building Nix config for WSL ğŸªŸ (fast mode)"
  sudo nixos-rebuild --flake .#joe-wsl switch
  @echo "âœ…  Built for WSL!"

[private]
build-bastion fast='':
  @echo "ğŸ”¨  Building Nix config for NixOS on Oracle Cloud ğŸ§"
  @{{ if fast != "--fast" { "just lint flake-update" } else { "echo \"ğŸš€ Fast mode, skipping checks...\"" } }}
  @{{ if fast != "--fast" { "echo \"ğŸ”  Checking Nix config for Oracle Cloud...\"" } else { "" } }}
  @{{ if fast != "--fast" { "NIXPKGS_ALLOW_UNSUPPORTED_SYSTEM=1 NIXPKGS_ALLOW_UNFREE=1 NIXPKGS_ALLOW_BROKEN=1 nix --extra-experimental-features 'nix-command flakes' flake check --impure --system aarch64-linux --show-trace" } else { "" } }}
  sudo nixos-rebuild --flake .#oracle-cloud-bastion switch
  @echo "âœ…  Built for NixOS on Oracle Cloud!"

[private]
build-bastion-fast:
  @echo "ğŸ”¨  Building Nix config for NixOS on Oracle Cloud ğŸ§ (fast mode)"
  sudo nixos-rebuild --flake .#oracle-cloud-bastion switch
  @echo "âœ…  Built for NixOS on Oracle Cloud!"

[unix]
deploy-racknerd IP:
  @echo "ğŸš€  Deploying Nix config to RackNerd VPS..."
  , nixos-anywhere --flake .#racknerd-cloud-agent --build-on local joe@{{IP}}
  @echo "âœ…  Deployed to RackNerd VPS!"

[unix]
rebuild-racknerd:
  @echo "ğŸ”¨  Rebuilding NixOS on RackNerd VPS..."
  sudo nixos-rebuild switch --flake .#racknerd-cloud-agent
  @echo "âœ…  Rebuilt RackNerd VPS!"

[private]
build-nixos fast='':
  @echo "ğŸ”¨  Building Nix config for NixOS ğŸ§"
  @{{ if fast != "--fast" { "just lint flake-update" } else { "echo \"ğŸš€ Fast mode, skipping checks...\"" } }}
  @{{ if fast != "--fast" { "echo \"ğŸ”  Checking Nix config for NixOS...\"" } else { "" } }}
  @{{ if fast != "--fast" { "NIXPKGS_ALLOW_UNSUPPORTED_SYSTEM=1 NIXPKGS_ALLOW_UNFREE=1 NIXPKGS_ALLOW_BROKEN=1 nix --extra-experimental-features 'nix-command flakes' flake check --impure --system x86_64-linux --show-trace" } else { "" } }}
  sudo nixos-rebuild --flake .#joe-desktop switch
  @echo "âœ…  Built for NixOS!"

[private]
build-nixos-fast:
  @echo "ğŸ”¨  Building Nix config for NixOS ğŸ§ (fast mode)"
  sudo nixos-rebuild --flake .#joe-desktop switch
  @echo "âœ…  Built for NixOS!"

[linux]
build fast='': system-info
  @if [[ "{{fast}}" != "" && "{{fast}}" != "-f" && "{{fast}}" != "--fast" ]]; then \
    echo "âŒ Error: Invalid 'fast' parameter '{{fast}}'. Valid options are: empty, '-f', or '--fast'"; \
    exit 1; \
  fi
  @if uname -r | grep -q "WSL"; then \
    just build-wsl $([ "{{fast}}" = "-f" ] && echo "--fast" || echo "{{fast}}"); \
  elif [ "{{arch()}}" = "aarch64" ]; then \
    just build-bastion $([ "{{fast}}" = "-f" ] && echo "--fast" || echo "{{fast}}"); \
  elif [ "{{arch()}}" = "x86_64" ]; then \
    just build-nixos $([ "{{fast}}" = "-f" ] && echo "--fast" || echo "{{fast}}"); \
  else \
    echo "âŒ Error: Unsupported architecture: {{arch()}}"; \
    exit 1; \
  fi

system-info:
  @echo "ğŸ–¥ï¸  This is an {{arch()}} machine on {{os()}}"

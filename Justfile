[private]
default: system-info
    @just --list

system-info:
    @echo "ðŸ–¥ï¸  This is an {{ arch() }} machine on {{ os() }}"

# â”€â”€ Core commands â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€

[unix]
check: lint
    @echo "ðŸ”  Checking Nix config for {{ arch() }}-{{ os() }}..."
    @NIXPKGS_ALLOW_UNSUPPORTED_SYSTEM=1 NIXPKGS_ALLOW_UNFREE=1 NIXPKGS_ALLOW_BROKEN=1 nix --extra-experimental-features 'nix-command flakes' flake check --impure --system {{ arch() }}-{{ os() }} --show-trace
    @echo "âœ…  Check completed!"

[unix]
lint:
    @echo "ðŸ“  Linting Nix config..."
    @nix --extra-experimental-features 'nix-command flakes' fmt
    @echo "âœ…  Nix config linted!"

[unix]
flake-update:
    @echo "ðŸ”„  Updating flake..."
    @nix --extra-experimental-features 'nix-command flakes' flake update --option access-tokens "github.com=$(gh auth token)"
    @echo "âœ…  Flake updated!"

# â”€â”€ Build â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€

[macos]
build: system-info
    darwin-rebuild build --flake .#Joes-MacBook-Pro 2>&1 | nom
    sudo darwin-rebuild switch --flake .#Joes-MacBook-Pro

[linux]
build: system-info
    @if uname -r | grep -q "WSL"; then \
      just _build-wsl; \
    elif [ "{{ arch() }}" = "aarch64" ]; then \
      just _build-bastion; \
    elif [ "{{ arch() }}" = "x86_64" ]; then \
      just _build-nixos; \
    else \
      echo "âŒ  Error: Unsupported architecture: {{ arch() }}"; \
      exit 1; \
    fi

[private]
_build-wsl:
    @echo "ðŸ”¨  Building for WSL ðŸªŸ..."
    nixos-rebuild build --flake .#joe-wsl 2>&1 | nom
    sudo nixos-rebuild --flake .#joe-wsl switch
    @echo "âœ…  Built for WSL!"

[private]
_build-bastion:
    @echo "ðŸ”¨  Building for Oracle Cloud bastion ðŸ§..."
    nixos-rebuild build --flake .#oracle-cloud-bastion 2>&1 | nom
    sudo nixos-rebuild --flake .#oracle-cloud-bastion switch
    @echo "âœ…  Built for Oracle Cloud!"

[private]
_build-nixos:
    @echo "ðŸ”¨  Building for NixOS desktop ðŸ§..."
    nixos-rebuild build --flake .#joe-desktop 2>&1 | nom
    sudo nixos-rebuild --flake .#joe-desktop switch
    @echo "âœ…  Built for NixOS!"

# â”€â”€ macOS-specific â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€

[macos]
build-macos-initial:
    @echo "ðŸ”¨  Building Nix config for macOS ðŸŽ (initial)..."
    sudo nix --extra-experimental-features 'nix-command flakes' run nix-darwin -- switch --flake .#Joes-MacBook-Pro
    @echo "âœ…  Built for macOS!"

[macos]
organize-launchpad:
    @echo "ðŸ”¨  Organizing Launchpad..."
    @lporg load --config $(pwd)/hosts/common/system/dotconfig/lporg.yaml --yes --no-backup
    @echo "âœ…  Organized Launchpad!"

[macos]
save-launchpad:
    @echo "ðŸ’¾  Saving Launchpad..."
    @lporg save --config $(pwd)/hosts/common/system/dotconfig/lporg.yaml
    @echo "âœ…  Saved Launchpad!"

# â”€â”€ Remote hosts â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€

[unix]
deploy-racknerd IP:
    @echo "ðŸš€  Deploying Nix config to RackNerd VPS..."
    , nixos-anywhere --flake .#racknerd-cloud-agent --build-on local joe@{{ IP }}
    @echo "âœ…  Deployed to RackNerd VPS!"

[unix]
rebuild-racknerd:
    @echo "ðŸ”¨  Rebuilding NixOS on RackNerd VPS..."
    nixos-rebuild build --flake .#racknerd-cloud-agent 2>&1 | nom
    sudo nixos-rebuild switch --flake .#racknerd-cloud-agent
    @echo "âœ…  Rebuilt RackNerd VPS!"

# â”€â”€ Package management â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€

[unix]
update-pins dry_run='':
    @echo "ðŸ”„  Updating pinned flake inputs..."
    @{{ if dry_run == "--dry-run" { "DRY_RUN=true" } else { "" } }} scripts/update-flake-pins.sh
    @echo "âœ…  Pins updated!"

[unix]
setup-python-packages packages='':
    @echo "ðŸ”„  Setting up Python packages..."
    @scripts/setup-python-packages.sh {{ packages }}
    @echo "âœ…  Python packages setup!"

[unix]
update-python-packages:
    @echo "ðŸ”„  Updating Python packages..."
    @scripts/update-python-packages.sh --no-build
    @echo "âœ…  Python packages updated!"

[unix]
update-node-packages:
    @echo "ðŸ”„  Updating Node packages..."
    @scripts/update-node-packages.sh
    @echo "âœ…  Node packages updated!"

# â”€â”€ Maintenance â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€

[unix]
nix-gc:
    @echo "ðŸ§¹  Garbage collecting nix..."
    @nix-env --delete-generations 14d
    @nix-store --gc
    @nix-collect-garbage -d
    @echo "âœ…  Garbage collected!"

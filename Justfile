[private]
default: system-info
  @just --list

[unix]
lint:
  @echo "ğŸ“  Linting Nix config..."
  @nix --extra-experimental-features 'nix-command flakes' fmt
  @echo "âœ…  Nix config linted!"

[unix]
check: lint flake-update
  @echo "ğŸ”  Checking Nix config..."
  @NIXPKGS_ALLOW_UNSUPPORTED_SYSTEM=1 NIXPKGS_ALLOW_UNFREE=1 NIXPKGS_ALLOW_BROKEN=1 nix --extra-experimental-features 'nix-command flakes' flake check --impure --all-systems
  @echo "âœ…  Flake check passed!"

[unix]
flake-update:
  @echo "ğŸ”„  Updating flake..."
  @nix --extra-experimental-features 'nix-command flakes' flake update
  @echo "âœ…  Flake updated!"

[unix]
nix-gc:
  @echo "ğŸ§¹  Garbage collecting nix..."
  @nix-env --delete-generations 14d
  @nix-store --gc
  @echo "âœ…  Garbage collected!"

build-zero2w-image:
  @echo "ğŸ”¨  Building Nix image for Raspberry Pi Zero 2 W ğŸ¥§"
  @ssh joe@bastion.joegold.in -- "cd dotfiles; nix build -L .#nixosConfigurations.zero2w-printer.config.system.build.sdImage"
  @scp joe@bastion.joegold.in:/home/joe/dotfiles/result/sd-image/zero2.img .
  @echo "âœ…  Built for Raspberry Pi Zero 2 W!"

build-zero2w-remote:
  @echo "ğŸ”¨  Building Nix config for Raspberry Pi Zero 2 W and deploying to remote ğŸ¥§"
  @nix run github:serokell/deploy-rs .#nixosConfigurations.zero2w-printer -- --ssh-user joe --hostname zero2w-printer.local
  @echo "âœ…  Built for Raspberry Pi Zero 2 W!"

[confirm]
[private]
build-macos: check
  @echo "ğŸ”¨  Building Nix config for macOS ğŸ"
  @nix --extra-experimental-features 'nix-command flakes' run nix-darwin -- switch --flake .#Joes-MacBook-Pro
  @echo "âœ…  Built for macOS!"

[macos]
organize-launchpad:
  @echo "ğŸ”¨  Organizing Launchpad..."
  @lporg load --config $(pwd)/environments/common/dotconfig/lporg.yaml --yes --no-backup
  @echo "âœ…  Organized Launchpad!"

[macos]
save-launchpad:
  @echo "ğŸ”¨  Organizing Launchpad..."
  @lporg save --config $(pwd)/environments/common/dotconfig/lporg.yaml
  @echo "âœ…  Saved Launchpad!"

[macos]
build: system-info
  @just build-macos

[confirm]
[private]
build-wsl: check
  @echo "ğŸ”¨  Building Nix config for WSL ğŸªŸ"
  @sudo nixos-rebuild --flake .#joe-wsl switch
  @echo "âœ…  Built for WSL!"

[confirm]
[private]
build-bastion: check
  @echo "ğŸ”¨  Building Nix config for NixOS on Oracle Cloud ğŸ§"
  @sudo nixos-rebuild --flake .#oracle-cloud-bastion switch
  @echo "âœ…  Built for NixOS on Oracle Cloud!"

[linux]
build: system-info
  @just {{ if shell('uname -r') =~ "WSL" { "build-wsl" } else { "build-bastion" } }}

system-info:
  @echo "ğŸ–¥ï¸  This is an {{arch()}} machine on {{os()}}"

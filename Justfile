[private]
default:
  @just --list

[unix]
lint:
  @echo "ğŸ“  Linting NixOS config..."
  @nix fmt
  @echo "âœ…  nix fmt passed!"

[unix]
check: lint flake-update
  @echo "ğŸ”  Checking NixOS config..."
  @NIXPKGS_ALLOW_UNSUPPORTED_SYSTEM=1 NIXPKGS_ALLOW_UNFREE=1 NIXPKGS_ALLOW_BROKEN=1 nix flake check --impure --all-systems
  @echo "âœ…  flake check passed!"

[unix]
flake-update:
  @echo "ğŸ”„  Updating flake..."
  @nix flake update

[confirm]
[private]
build-macos: check
  @echo "ğŸ”¨  Building NixOS config for macOS ğŸ"
  @nix run --extra-experimental-features 'nix-command flakes' nix-darwin -- switch --flake .#Joes-MacBook-Pro

[macos]
organize-launchpad:
  @echo "ğŸ”¨  Organizing Launchpad..."
  @lporg load --config $(pwd)/environments/common/dotconfig/lporg.yaml --yes --no-backup

[macos]
save-launchpad:
  @echo "ğŸ”¨  Organizing Launchpad..."
  @lporg save --config $(pwd)/environments/common/dotconfig/lporg.yaml

[macos]
build:
  @just build-macos

[confirm]
[private]
build-wsl: check
  @echo "ğŸ”¨  Building NixOS config for WSL ğŸªŸ"
  @sudo nixos-rebuild --flake .#joe-wsl switch

[confirm]
[private]
build-bastion: check
  @echo "ğŸ”¨  Building NixOS config for NixOS ğŸ§"
  @sudo nixos-rebuild --flake .#oracle-cloud-bastion switch

[linux]
build:
  @just {{ if shell('uname -r') =~ "WSL" { "build-wsl" } else { "build-bastion" } }}

system-info:
  @echo "ğŸ–¥ï¸  This is an {{arch()}} machine on {{os()}}"

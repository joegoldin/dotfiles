[private]
default: system-info
  @just --list

system-info:
  @echo "This is an {{arch()}} machine on {{os()}}"

# ── Core commands ────────────────────────────────────────────────────────

[unix]
check:
  @echo "Checking Nix config for {{arch()}}-{{os()}}..."
  @NIXPKGS_ALLOW_UNSUPPORTED_SYSTEM=1 NIXPKGS_ALLOW_UNFREE=1 NIXPKGS_ALLOW_BROKEN=1 nix --extra-experimental-features 'nix-command flakes' flake check --impure --system {{arch()}}-{{os()}} --show-trace
  @echo "Check completed!"

[unix]
lint:
  @echo "Linting Nix config..."
  @nix --extra-experimental-features 'nix-command flakes' fmt
  @echo "Nix config linted!"

[unix]
flake-update:
  @echo "Updating flake..."
  @nix --extra-experimental-features 'nix-command flakes' flake update --option access-tokens "github.com=$(gh auth token)"
  @echo "Flake updated!"

# ── Build ────────────────────────────────────────────────────────────────

[macos]
build: system-info
  darwin-rebuild build --flake .#Joes-MacBook-Pro 2>&1 | nom
  sudo darwin-rebuild switch --flake .#Joes-MacBook-Pro

[linux]
build: system-info
  @if uname -r | grep -q "WSL"; then \
    just _build-wsl; \
  elif [ "{{arch()}}" = "aarch64" ]; then \
    just _build-bastion; \
  elif [ "{{arch()}}" = "x86_64" ]; then \
    just _build-nixos; \
  else \
    echo "Error: Unsupported architecture: {{arch()}}"; \
    exit 1; \
  fi

[private]
_build-wsl:
  @echo "Building for WSL..."
  nixos-rebuild build --flake .#joe-wsl 2>&1 | nom
  sudo nixos-rebuild --flake .#joe-wsl switch

[private]
_build-bastion:
  @echo "Building for Oracle Cloud bastion..."
  nixos-rebuild build --flake .#oracle-cloud-bastion 2>&1 | nom
  sudo nixos-rebuild --flake .#oracle-cloud-bastion switch

[private]
_build-nixos:
  @echo "Building for NixOS desktop..."
  nixos-rebuild build --flake .#joe-desktop 2>&1 | nom
  sudo nixos-rebuild --flake .#joe-desktop switch

# ── macOS-specific ───────────────────────────────────────────────────────

[macos]
build-macos-initial:
  @echo "Building Nix config for macOS (initial)..."
  sudo nix --extra-experimental-features 'nix-command flakes' run nix-darwin -- switch --flake .#Joes-MacBook-Pro

[macos]
organize-launchpad:
  @echo "Organizing Launchpad..."
  @lporg load --config $(pwd)/hosts/common/system/dotconfig/lporg.yaml --yes --no-backup

[macos]
save-launchpad:
  @echo "Saving Launchpad..."
  @lporg save --config $(pwd)/hosts/common/system/dotconfig/lporg.yaml

# ── Remote hosts ─────────────────────────────────────────────────────────

[unix]
deploy-racknerd IP:
  @echo "Deploying Nix config to RackNerd VPS..."
  , nixos-anywhere --flake .#racknerd-cloud-agent --build-on local joe@{{IP}}

[unix]
rebuild-racknerd:
  @echo "Rebuilding NixOS on RackNerd VPS..."
  nixos-rebuild build --flake .#racknerd-cloud-agent 2>&1 | nom
  sudo nixos-rebuild switch --flake .#racknerd-cloud-agent

# ── Package management ───────────────────────────────────────────────────

[unix]
update-pins dry_run='':
  @echo "Updating pinned flake inputs..."
  @{{ if dry_run == "--dry-run" { "DRY_RUN=true" } else { "" } }} scripts/update-flake-pins.sh

[unix]
setup-python-packages packages='':
  @echo "Setting up Python packages..."
  @scripts/setup-python-packages.sh {{packages}}

[unix]
update-python-packages:
  @echo "Updating Python packages..."
  @scripts/update-python-packages.sh --no-build

[unix]
update-node-packages:
  @echo "Updating Node packages..."
  @scripts/update-node-packages.sh

# ── Maintenance ──────────────────────────────────────────────────────────

[unix]
nix-gc:
  @echo "Garbage collecting nix..."
  @nix-env --delete-generations 14d
  @nix-store --gc
  @nix-collect-garbage -d
  @echo "Garbage collected!"
